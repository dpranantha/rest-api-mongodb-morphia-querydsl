group 'restful-service-mongodb'
version '1.0'

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'war'
apply plugin: 'jetty'

jar {
    baseName = 'restful-service-mongodb'
    version =  '0.0.1'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile 'org.springframework:spring-core:4.1.9.RELEASE'
    compile 'org.springframework:spring-context:4.1.9.RELEASE'
    compile 'org.springframework:spring-beans:4.1.9.RELEASE'
    compile 'org.springframework:spring-jdbc:4.1.9.RELEASE'
    compile 'org.springframework:spring-web:4.1.9.RELEASE'

    compile 'javax.servlet:javax.servlet-api:3.1.0'
    compile 'org.glassfish.jersey.containers:jersey-container-servlet-core:2.12'
    compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.12'

    compile 'org.apache.commons:commons-lang3:3.4'

    compile 'com.querydsl:querydsl-sql:4.0.6'
    compile 'com.querydsl:querydsl-sql-spring:4.0.6'
    compile 'com.querydsl:querydsl-sql-codegen:4.0.6'
    compile 'com.mysema.querydsl:querydsl-mongodb:3.7.0'
    compile 'org.mongodb.morphia:morphia:1.0.1'
    compile 'org.mongodb:mongo-java-driver:3.2.0'
    compile 'org.springframework.data:spring-data-mongodb:1.8.2.RELEASE'
    compile 'com.mysema.querydsl:querydsl-apt:3.7.0'
    compile 'com.mysema.querydsl:querydsl-jpa:3.7.0'

    compile 'com.google.guava:guava:18.0'
    compile 'org.bitbucket.cowwoc:guava-jdk8:1.1'
    compile 'org.jooq:jool:0.9.7'
    compile 'io.atlassian.fugue:fugue:3.1.0'

    compile 'org.codehaus.jackson:jackson-jaxrs:1.9.13'

    compile 'org.eclipse.jetty:jetty-server:9.3.6.v20151106'
    compile 'org.eclipse.jetty:jetty-servlet:9.3.6.v20151106'
    compile 'org.apache.cxf:cxf-rt-frontend-jaxrs:3.1.4'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.springframework:spring-test:4.1.6.RELEASE'
    testCompile 'com.jayway.restassured:rest-assured:2.8.0'
    testCompile 'com.jayway.restassured:spring-mock-mvc:2.8.0'
}

sourceSets {
    generated {
        java {
            srcDirs = ['src/main/generated']
        }
    }
}

configurations {
    querydslapt
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generates the QueryDSL query types') {
    source = sourceSets.main.java
    classpath = configurations.compile + configurations.querydslapt
    options.compilerArgs = [
            "-proc:only",
            "-processor", "org.springframework.data.mongodb.repository.support.MongoAnnotationProcessor"
    ]
    destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
    dependsOn generateQueryDSL
    source generateQueryDSL.destinationDir
}

compileGeneratedJava {
    dependsOn generateQueryDSL
    options.warnings = false
    classpath += sourceSets.main.runtimeClasspath
}

clean {
    delete sourceSets.generated.java.srcDirs
}

idea {
    module {
        sourceDirs += file('src/main/generated')
    }
}

assemble {
    doLast {
        copy {
            from "${rootDir}/src/main/resources"
            include 'rsm.properties'
            into "${buildDir}/properties"
        }
    }
}

//for jettyRun
Properties props = new Properties()
props.load(new FileInputStream("src/main/resources/rsm.properties"))

httpPort=props.getProperty("rsm.main.port").toInteger()
stopPort=props.getProperty("rsm.stop.port").toInteger()
stopKey = "stopKey"